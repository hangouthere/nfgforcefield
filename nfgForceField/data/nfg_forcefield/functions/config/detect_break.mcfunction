execute if entity @s[tag=ff_config_saved] run tellraw @s [{"text":"[nfgForceField] ","color":"gold"},{"text":"Saved ","color":"white"},{"text":"ForceField ","color":"gold"},{"text":"configuration!","color":"white"}]

# Kill junk that was left behind from breaking the chest
kill @e[sort=nearest,distance=..8,type=item,nbt={Item:{tag:{ff:{config:1b}}}}]
kill @e[sort=nearest,distance=..8,type=item,nbt={Item:{tag:{ff:{messages:1b}}}}]
#TODO: Determine if we want this
#kill @e[sort=nearest,distance=..8,type=item,nbt={Item:{tag:{ff:{potion:1b}}}}]
kill @e[sort=nearest,distance=..8,type=item,nbt={Item:{id:"minecraft:chest"}}]

# Clear Players Inventory of all Books generated by the Configurator
clear @a writable_book{ff:{messages:1b}}

# Destroy the `ff_config_marker`
kill @e[tag=ff_config_marker,sort=nearest,limit=1]

tag @s remove ff_config_init
tag @s remove ff_config_active
tag @s remove ff_config_inactive
tag @s remove ff_config_saved

#Return non-Configure items to the Player for a Page
function nfg_forcefield:config/pages/page_return

# Give the Player an Item to place another Configurator
execute if entity @s[gamemode=!creative,gamemode=!spectator] run function nfg_forcefield:inventory/give_config_marker
